
/*SPOTIFY 2023 ANALYSIS*/

/*CREATING LIBRARY TO SAVE THE DATASETS*/
LIBNAME SPOTIFY "G:\SAS\Projects\spotify-2023";


/*IMPORTING DATASET FROM A CSV FILE USING PROC IMPORT*/
PROC IMPORT DATAFILE="G:\SAS\Projects\spotify-2023\spotify dataset-2023.csv"
						  OUT=SPOTIFY.DATASET
						  DBMS=CSV
						  REPLACE;
			GETNAMES=YES;
RUN;


/*REMOVING DUPLICATE VALUES USING  PROC SORT*/
PROC SORT DATA=SPOTIFY.DATASET OUT=SPOTIFY.CLEANED_DATA NODUPRECS;
BY _ALL_;
RUN;


/* FIND TOP 10 TRACKS BASED ON THE NUMBER OF STREAMS .*/
PROC SORT DATA=SPOTIFY.CLEANED_DATA OUT=SPOTIFY.TOP10_TRACKS ;
BY DESCENDING STREAMS ;
RUN;

TITLE "TOP 10 TRACKS BY STREAMS";
PROC PRINT DATA=SPOTIFY.TOP10_TRACKS (OBS=10);
var track_name artist_s__name streams;
RUN;

/*CODE TO CREATE GRAPH*/
PROC SGPLOT DATA=SPOTIFY.TOP10_TRACKS (OBS=10);
	VBAR TRACK_NAME / RESPONSE=STREAMS;
	TITLE "TOP 10 TRACKS BY STREAMS";
RUN;



/*FIND TOP 10 ARTIST'S BASED ON STREAMS*/

PROC SORT DATA=SPOTIFY.CLEANED_DATA OUT=ARTIST;
BY artist_s__name ;
RUN;

DATA  SPOTIFY.TOP10_ARTIST;
SET ARTIST (KEEP= artist_s__name STREAMS);
BY artist_s__name;
IF FIRST.artist_s__name THEN TOTAL_STREAMS=0;
TOTAL_STREAMS + STREAMS;
IF LAST.artist_s__name;
RUN;

PROC SORT DATA=SPOTIFY.TOP10_ARTIST;
BY DESCENDING TOTAL_STREAMS;
RUN;

PROC PRINT DATA=SPOTIFY.TOP10_ARTIST (OBS=10);
TITLE "Top 10 Artists by Total Streams";
VAR artist_s__name TOTAL_STREAMS;
RUN;

/*CODE TO CREATE GRAPH*/
PROC SGPLOT DATA=SPOTIFY.TOP10_ARTIST (OBS=10);
	VBAR ARTIST_S__NAME / RESPONSE=TOTAL_STREAMS;
	TITLE "Top 10 Artists by Total Streams";
RUN;


/*ALTERNATIVE WAY UISNG PROC SQL */
PROC SQL;
    CREATE TABLE SPOTIFY.Artist_Streams AS
    SELECT artist_s__name, SUM(streams) AS TOTAL_STREAMS
    FROM Spotify.Cleaned_data
    GROUP BY artist_s__name
    ORDER BY TOTAL_STREAMS DESC;
QUIT;

PROC PRINT DATA=SPOTIFY.Artist_Streams (OBS=10);
    VAR artist_s__name TOTAL_STREAMS;
    TITLE "Top 10 Artists by Total Streams";
RUN;

/*CODE TO CREATE GRAPH*/
PROC SGPLOT DATA=SPOTIFY.ARTIST_STREAMS (OBS=10);
	VBAR ARTIST_S__NAME / RESPONSE= TOTAL_STREAMS;
	TITLE "Top 10 Artists by Total Streams";
RUN;



/*Compare the performance of solo vs. collaborative tracks*/
TITLE "Comparison of Solo vs Collaborative Tracks by Average Streams";
PROC MEANS DATA=SPOTIFY.CLEANED_DATA MAXDEC= 0 MEAN;
OUTPUT OUT=SPOTIFY.COMPARISON  MEAN=MEAN;
CLASS ARTIST_COUNT;
VAR STREAMS;
RUN;

DATA SPOTIFY.COMPARISON;
SET SPOTIFY.COMPARISON;
IF ARTIST_COUNT = . THEN DELETE;
RUN;

/*CODE TO CREATE GRAPH*/
PROC SGPLOT DATA=SPOTIFY.COMPARISON;
	VBAR ARTIST_COUNT / RESPONSE=MEAN;
	TITLE "Comparison of Solo vs Collaborative Tracks by Average Streams";
RUN;


/* SEARCH TOP 5 SONGS OF ANY SINGER (IF PRESENT IN DATASET)*/
PROC SORT DATA = SPOTIFY.CLEANED_DATA;
BY DESCENDING STREAMS ;
RUN;

/*CREATED USER-DEFINED MACRO*/
/*ONLY CHANGE THE NAME OF SINGER IN BELOW PARENTHESIS*/
%LET SINGER_NAME = Ed Sheeran;
DATA SPOTIFY.FIND_SONG;
SET SPOTIFY.CLEANED_DATA;
WHERE artist_s__name CONTAINS   "&SINGER_NAME";
RUN;

PROC PRINT DATA=SPOTIFY.FIND_SONG (OBS=5);
TITLE "TOP 10 SONGS OF &SINGER_NAME  ";
VAR track_name artist_s__name  released_year  in_spotify_playlists in_spotify_charts streams; 
RUN;


/*SEARCH SONG USING SONG NAME*/
/*ONLY CHANGE THE NAME OF SONG IN BELOW PARENTHESIS*/
%LET SONG_NAME=Wake Me Up - Radio Edit;
%put &song_name; 
DATA SPOTIFY.FIND_SONG;
SET SPOTIFY.CLEANED_DATA;
WHERE TRACK_NAME CONTAINS "&SONG_NAME";
RUN;

PROC PRINT DATA=SPOTIFY.FIND_SONG (OBS=10);
TITLE1 "THE RESULTS ARE SHOWING FOR  &SONG_NAME ";
VAR track_name artist_s__name  released_year  in_spotify_playlists in_spotify_charts streams; 
RUN;


/*TO FIND NO. OF SONGS RELEASED PER MONTH*/
PROC FREQ DATA=SPOTIFY.CLEANED_DATA;
TITLE "NUMBER OF SONGS RELEASED BY MONTHS";
TABLE RELEASED_MONTH /  nocum OUT=SPOTIFY.MONTHWISE ;
RUN;

PROC FORMAT ;
	VALUE MONTHS  1="JAN"
								2="FEB"
								3="MAR"
								4="APR"
								5="MAY"
								6="JUN"
								7="JUL"
								8="AUG"
								9="SEP"
								10="OCT"
								11="NOV"
								12="DEC";
	RUN;


DATA SPOTIFY.MONTHWISE;
SET SPOTIFY.MONTHWISE;
FORMAT RELEASED_MONTH MONTHS. ;
LABEL COUNT="NO. OF SONGS" 
			PERCENT="PERSENTAGE"
			RELEASED_MONTH="RELEASED MONTH";
RUN;

PROC SGPLOT DATA=SPOTIFY.MONTHWISE;
    VBAR RELEASED_MONTH / RESPONSE= COUNT DATALABEL;
    XAXIS LABEL="Release month";
    YAXIS LABEL="NO. OF SONGS";
    TITLE "Distribution of Songs by Release Month";
RUN;

